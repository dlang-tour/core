# Ассоциативные массивы

В языке D есть встроенные *ассоциативные массивы*, также известные как
хэш-таблицы. Ассоциативный массив с ключом типа `int` и значением типа `string`
объявляется так:

    int[string] arr;

Доступ к значению можно получить по его ключу и установить таким образом:

    arr["key1"] = 10;

Для проверки, существует ли ключ в ассоциативном массиве, может быть
использовано выражение `in`:

    if ("key1" in arr)
        writeln("Yes");

Выражение `in` возвращает указатель на значение, которое не является `null`,
если ключ найден. Такая проверка и запись значения могут быть легко объединены:

    if (auto val = "key1" in arr)
        *val = 20;

Доступ к несуществующему ключу приведёт к ошибке `RangeError`, что немедленно
прервёт выполнение приложения. Для безопасного доступа с заданными по умолчанию
значениями, можно использовать `get(key, defaultValue)`.

Подобно обычным массивам, у ассоциативных массивов есть свойство `.length` и
метод `.remove(val)` для удаления записи по её ключу. В качестве упражнения,
читателю предлагается самостоятельно исследовать специальные диапазоны `.byKeys`
и `.byValues`.

### Подробнее

- [Ассоциативные массивы в _Programming in D_](http://ddili.org/ders/d.en/aa.html)
- [Спецификация по ассоциативным массивам](https://dlang.org/spec/hash-map.html)
- [std.array.byPair](http://dlang.org/phobos/std_array.html#.byPair)

## {SourceCode}

```d
import std.stdio;

/**
Разделяет заданный текст на слова и возвращает
ассоциативный массив, сопоставляющий слова с
их количеством (сколько раз какое слово
использовалось)

Параметры:
    text = текст для разделения
*/
int[string] wordCount(string text)
{
    // Функция разделения лениво разделяет
    // входные данные в диапазон
    import std.algorithm.iteration: splitter;
    import std.string: toLower;

    // Проиндексированно по словам и возвращает
    // количество
    int[string] words;

    foreach(word; splitter(text.toLower(), " "))
    {
        // Прибавляет счётчик слова, если оно
        // было найдено.
        // Целые числа по умолчанию равны 0.
        words[word]++;
    }

    return words;
}

void main()
{
    string text = "D is a lot of fun";

    writeln("Word counts: ", wordCount(text));
}
```
